# SPDX-FileCopyrightText: 2025 Laurent Montel <montel@kde.org>
# SPDX-License-Identifier: BSD-3-Clause
cmake_minimum_required(VERSION 3.16.0 FATAL_ERROR)
set(KAICHAT-ADDONS_VERSION "0.2")
project(kaichat-addons VERSION ${KAICHAT-ADDONS_VERSION})

set(QT_REQUIRED_VERSION "6.9.0")
set(KF_MIN_VERSION "6.19.0")
set(CMAKE_CXX_SCAN_FOR_MODULES OFF)

set(ECM_VERSION ${KF_MIN_VERSION})
find_package(ECM ${ECM_VERSION} REQUIRED NO_MODULE)
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${ECM_MODULE_PATH})
include(FeatureSummary)
file(GLOB_RECURSE ALL_CLANG_FORMAT_SOURCE_FILES *.cpp *.h *.c)

set(PRE_COMMIT_INSTALLED FALSE)
if(${CMAKE_SOURCE_DIR} STREQUAL ${PROJECT_SOURCE_DIR})
    if(EXISTS "${PROJECT_SOURCE_DIR}/.git/hooks/pre-commit")
        file(READ "${PROJECT_SOURCE_DIR}/.git/hooks/pre-commit" FILE_CONTENTS)
        string(FIND "${FILE_CONTENTS}" "File generated by pre-commit" INDEX)
        if(${INDEX} GREATER_EQUAL 0)
            set(PRE_COMMIT_INSTALLED TRUE)
        endif()
    endif()
endif()
if(NOT ${PRE_COMMIT_INSTALLED})
    find_program(PRE_COMMIT_EXECUTABLE pre-commit)
    if(PRE_COMMIT_EXECUTABLE)
        message(
            STATUS
            "This module has pre-commit support. pre-commit binary was found. Installing pre-commit hook"
        )
        execute_process(
            COMMAND "${PRE_COMMIT_EXECUTABLE}" install -f
            ERROR_QUIET
            RESULT_VARIABLE _precommitresult
            WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}"
        )
        if(_precommitresult EQUAL 0)
            message(STATUS "Pre-commit installed.")
        else()
            message(
                WARNING
                "Error during installing pre-commit hook: ${PRE_COMMIT_EXECUTABLE} project source dir ${PROJECT_SOURCE_DIR}"
            )
            include(KDEGitCommitHooks)
            include(KDEClangFormat)
            kde_clang_format(${ALL_CLANG_FORMAT_SOURCE_FILES})
        endif()
    else()
        message(
            WARNING
            "This module has pre-commit support. pre-commit binary was not found."
        )
        include(KDEGitCommitHooks)
        include(KDEClangFormat)
        kde_clang_format(${ALL_CLANG_FORMAT_SOURCE_FILES})
    endif()
endif()

include(ECMAddAppIcon)
include(ECMInstallIcons)
include(KDEInstallDirs)
include(KDECompilerSettings NO_POLICY_SCOPE)
include(KDECMakeSettings)
include(ECMAddTests)
include(ECMQtDeclareLoggingCategory)
include(GenerateExportHeader)
include(ECMDeprecationSettings)
include(ECMCheckOutboundLicense)

set(KTEXTADDONS_WITH_TEXTAUTOGENERATETEXT_VERSION "1.8.67")

find_package(
    Qt6
    ${QT_REQUIRED_VERSION}
    REQUIRED
    NO_MODULE
    COMPONENTS Widgets Network
)
find_package(
    KF6
    ${KF_MIN_VERSION}
    REQUIRED
    COMPONENTS CoreAddons I18n Config KIO
)
find_package(
    KF6TextAutoGenerateText
    ${KTEXTADDONS_WITH_TEXTAUTOGENERATETEXT_VERSION}
    CONFIG
)
set_package_properties(
    KF6TextAutoGenerateText
    PROPERTIES
        TYPE REQUIRED
        PURPOSE "Required for building AI plugins support"
        DESCRIPTION "Use AI plugin"
)

add_definitions(-DQT_NO_CONTEXTLESS_CONNECT)
include_directories(${CMAKE_CURRENT_BINARY_DIR})
ecm_set_disabled_deprecation_versions(QT 6.11.0
    KF 6.21.0
)

if(BUILD_TESTING)
    find_package(Qt6 ${QT_REQUIRED_VERSION} REQUIRED COMPONENTS Test)
    add_definitions(-DBUILD_TESTING)
endif()

option(
    USE_UNITY_CMAKE_SUPPORT
    "Use UNITY cmake support (speedup compile time)"
    OFF
)

set(COMPILE_WITH_UNITY_CMAKE_SUPPORT OFF)
if(USE_UNITY_CMAKE_SUPPORT)
    set(COMPILE_WITH_UNITY_CMAKE_SUPPORT ON)
endif()

find_package(KWeatherCore)
set_package_properties(
    KWeatherCore
    PROPERTIES DESCRIPTION "Add tools plugins weather plugin" TYPE OPTIONAL
)

if(TARGET KWeatherCore)
    add_subdirectory(weather)
endif()

add_subdirectory(wikipedia)
add_subdirectory(websearch)

file(
    GLOB_RECURSE ALL_SOURCE_FILES
    wikipedia/*.cpp
    wikipedia/*.h
    websearch/*.cpp
    websearch/*.h
    weather/*.cpp
    weather/*.h
)
ecm_check_outbound_license(LICENSES GPL-2.0-only  FILES ${ALL_SOURCE_FILES})
if(DEFINED kde_configure_git_pre_commit_hook)
    kde_configure_git_pre_commit_hook(CHECKS CLANG_FORMAT)
endif()
ecm_qt_install_logging_categories(
    EXPORT KAICHAT-ADDONS
    FILE kaichat-addons.categories
    DESTINATION ${KDE_INSTALL_LOGGINGCATEGORIESDIR}
)

ki18n_install(po)
feature_summary(WHAT ALL FATAL_ON_MISSING_REQUIRED_PACKAGES)
